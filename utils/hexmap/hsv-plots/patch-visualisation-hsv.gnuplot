# Gnuplot script to generate a visualisation of the distribution of flowers in the environment at a
# given point in time, using HSV colour space. 
#
# The plot takes as input a datafile generated by the gen-data-for-patch-visualisation-hsv.awk script
#
# Call like this, e.g.
# > gnuplot -c patch-visualisation-hsv.gnuplot filebase gen [envsize (default=300)] [titletext]
# e.g.
# > gnuplot -c patch-visualisation-hsv.gnuplot flower-positions-hsv 0
#
# The script looks for an input data file named [filebase]-gen-N.csv
#
# The output is saved in a file named [filebase]-gen-000N.png
#

filebase="flower-positions-hsv"
gen=0
envsize=300
titletext="Flower Distribution at "

if (exists("ARG1")) {
    filebase=ARG1
}

if (exists("ARG2")) {
    gen=ARG2
}

if (exists("ARG3")) {
    envsize=ARG3
}

if (exists("ARG4")) {
    titletext=ARG4." / "
}

infile = sprintf("%s-gen-%d.csv", filebase, gen)
outfile = sprintf("%s-gen-%04d.png", filebase, gen)

set terminal png size 800,800
set output outfile

set size square
set datafile separator ","
set xrange [0:envsize]
set yrange [envsize:0]
set title titletext."Generation ".gen
unset key
set xtics 20
set ytics 20

plot infile using 1:2:(hsv2rgb($7/360.0,0.3+$9/0.7,1)) with points pt 7 ps 0.5 lc rgbcolor variable
