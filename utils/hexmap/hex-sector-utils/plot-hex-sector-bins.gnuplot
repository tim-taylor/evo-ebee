# Gnuplot script to generate a histogram plot of flower data with flowers laid
# out in hex sector space in bins of 10 degrees, for all flower types present in a 
# simulation run at the specified generation number, using input data from the
# specified file.
#
# The input file is a 4-field CSV file as generated by the utility script
# gen-hex-sector-bin-means-stds
#
# Call like this, e.g.
# > gnuplot -c plot-hex-sector-bins.gnuplot filebase N titletext
# e.g.
# > gnuplot -c plot-hex-sector-bins.gnuplot hex-sector-bin-means-stds 0
#
# The script looks for an input data file named [filebase]-gen-N.csv
#
# The output is saved in a file named [filebase]-gen-N-hex-sector.png

filebase="hex-sector-bin-means-stds"
gen=0
titletext=""

if (exists("ARG1")) {
    filebase=ARG1
}

if (exists("ARG2")) {
    gen=ARG2
}

if (exists("ARG3")) {
    titletext=ARG3." / "
}

fbaseIN = sprintf("%s-gen-%s", filebase, gen)
infile = sprintf("%s.csv", fbaseIN)
fbaseOUT = sprintf("%s-gen-%04d", filebase, (gen+0))
outfile = sprintf("%s.png", fbaseOUT)

set datafile separator ","
set size square
set title font ",13"
set title titletext."Generation ".(gen+1)
set xtics nomirror out 20
set xtics font ",11"
set ytics nomirror out
set boxwidth 6 absolute
set style fill solid 1.0 border lc "gray30"
unset key
set xrange[0:365]
set yrange[0:22500]
set term png size 800,800
set output outfile
plot infile using (($1*10)+10):3:4 with boxerrorbars fillcolor "gray70"
