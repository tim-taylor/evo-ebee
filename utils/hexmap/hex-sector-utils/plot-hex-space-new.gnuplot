# Gnuplot script to generate a plot of flower positions in hex space
# for all flower types present in a simulation run at the specified
# generation number, using input data from the specified file.
#
# The input file is a 7-field CSV file as generated by the utility script
# gen-hex-plot-data or gen-hex-plot-data-across-runs
#
# Call like this, e.g.
# > gnuplot -c plot-hex-space.gnuplot filebase N titletext 
# e.g.
# > gnuplot -c plot-hex-space.gnuplot hex-info 100 "Australian dataset"
#
# The script looks for an input data file named [filebase]-gen-N.csv
#
# The output is saved in a file named [filebase]-gen-N-hex-space.png


filebase="hex-info"
gen=0
titletext=""

if (exists("ARG1")) {
    filebase=ARG1
}

if (exists("ARG2")) {
    gen=ARG2
}

if (exists("ARG3")) {
    titletext=ARG3." / "
}


fbase = sprintf("%s-gen-%s", filebase, gen)
infile = sprintf("%s.csv", fbase)
outfile = sprintf("%s-hex-space.png", fbase)

set datafile separator ","
set size square
set xrange[-0.9:0.9]
set yrange[-0.9:0.9]
set grid
unset xlabel
unset ylabel
set title titletext."Generation ".gen
unset key
set palette gray negative
set term png size 800,600
set output outfile
# if field 5 (num landings) is zero, set xpos to NaN so that gnuplot silently ignores this line
plot infile using ($5 == 0 ? NaN : $2):3:5 palette pt 7 ps 1.1
